buildscript {
    apply from: "$rootProject.projectDir/../gradle/lib/buildscript.gradle", to: buildscript
}

apply plugin: 'java-gradle-plugin'
apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'maven-publish'

description = 'Javarel Tooling - Gradle Javarel Plugin'
defaultTasks = ['publishToMavenLocal']

configurations {
    felixBin { transitive = false }
}

jar {
    from configurations.felixBin, { it.into("com/neva/javarel/gradle/instance/distribution/overrides/bin") }

    manifest {
        attributes([
                'Implementation-Title'  : project.description,
                'Implementation-Version': project.version
        ])
    }
}

dependencies {
    felixBin 'org.jetbrains.kotlin:kotlin-stdlib:1.1.4'
    felixBin project(':felix-launcher')

    compile gradleApi()

    compile 'org.jetbrains.kotlin:kotlin-stdlib:1.1.4'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'commons-io:commons-io:2.4'
    compile 'commons-httpclient:commons-httpclient:3.1'
    compile 'commons-validator:commons-validator:1.6'
    compile 'com.fasterxml.jackson.module:jackson-module-kotlin:2.8.8'
    compile 'org.reflections:reflections:0.9.9'
    compile 'org.dm.gradle:gradle-bundle-plugin:0.10.0'
    compile 'org.apache.jackrabbit.vault:vault-cli:3.1.24'
    compile 'org.jsoup:jsoup:1.10.3'
    compile 'org.samba.jcifs:jcifs:1.3.18-kohsuke-1'
    compile group: 'com.google.guava', name: 'guava', version: '23.0'
    compile group: 'org.zeroturnaround', name: 'zt-zip', version: '1.11'
    compile group: 'com.hierynomus', name: 'sshj', version: '0.21.1'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.3'
    compile group: 'org.osgi', name: 'org.osgi.core', version: '6.0.0'
    compile group: 'com.mitchellbosecke', name: 'pebble', version: '2.4.0'

    testCompile gradleTestKit()
    testCompile 'junit:junit:4.12'
}

gradlePlugin {
    plugins {
        base {
            id = "com.neva.javarel.base"
            implementationClass = "com.neva.javarel.gradle.BasePlugin"
        }
        bundle {
            id = "com.neva.javarel.bundle"
            implementationClass = "com.neva.javarel.gradle.BundlePlugin"
        }
        instance {
            id = "com.neva.javarel.instance"
            implementationClass = "com.neva.javarel.gradle.InstancePlugin"
        }
    }
}